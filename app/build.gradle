plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}
// 加载仓库的密钥配置
// 存放安卓密钥和配置的项目名称
def keystoreProject = "AndroidKeystore"
def keystorePropsName = "juice.properties"
def keystoreFileName = "juice.jks"
def keystorePropsPath = "$rootDir.parentFile/$keystoreProject/$keystorePropsName"
def keystoreFilePath = "$rootDir.parentFile/$keystoreProject/$keystoreFileName"
//println "安卓密钥配置的路径：${keystorePropsPath}"
//println "安卓密钥的路径：${keystoreFilePath}"

// 密钥配置
def keyProps = new Properties()
def keyPropsFile = rootProject.file(keystorePropsPath)
if (keyPropsFile.exists()) {
    keyProps.load(new FileInputStream(keyPropsFile))
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.juice.timetable"
        minSdk 28
        targetSdk 32
        versionCode 1
        versionName "3.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // 配置打包使用的密钥
    signingConfigs {
        juice {
            keyAlias keyProps['keyAlias']
            keyPassword keyProps['keyPassword']
            storeFile file(keystoreFilePath)
            storePassword keyProps['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            // 配置签名
            signingConfig signingConfigs.juice
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            debuggable false
            //是否混淆
            minifyEnabled true
            //是否去除无效的资源文件
            shrinkResources true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation("com.squareup.okhttp3:okhttp:4.9.3")
    implementation "com.squareup.okhttp3:logging-interceptor:4.7.2"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"

    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
}